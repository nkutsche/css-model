/* 
Copyright (c) 2015, transpect.io
All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

* Redistributions of source code must retain the above copyright notice, this
  list of conditions and the following disclaimer.

* Redistributions in binary form must reproduce the above copyright notice,
  this list of conditions and the following disclaimer in the documentation
  and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE. 
*/

css      ::= S* ( (  rule | simple_atrule | import | mediaquery | printcssquery | pagequery | COMMENT) S* )*
simple_atrule  ::= ('@charset'|'@namespace') S+ QUOTED_STRING S* ';' 
import ::= '@import' S+ ( QUOTED_STRING | URL ) S* ';'
rule     ::= selectors_group S? '{' S* (COMMENT? S* declaration ( S? ';' S? declaration?)* ';'? S?)* COMMENT* S* '}'
printcssquery ::= printcssrule S? '{' ( S? COMMENT* S? (pagequery| COMMENT|query_declaration) S*)* S? '}'
mediaquery ::= mediarule S? '{' ( S? COMMENT* query_declaration)* S? '}'
pagequery ::= pagerule S? '{' S* (COMMENT? S* page_declaration ( S? ';' S? page_declaration?)* ';'? S?)* COMMENT* S* '}'
page_declaration ::= (areaquery|declaration)
areaquery ::= arearule S? '{' S* (COMMENT? S* declaration ( S? ';' S? declaration?)* ';'? S?)* COMMENT* S* '}'
query_declaration ::= rule
declaration ::=  (property S? ':'  (property ':')* S? values) 
selectors_group
         ::= selector ( COMMENT? COMMA S* selector )*  
selector ::= simple_selector_sequence ( combinator simple_selector_sequence )*
mediaquery_selector::= '(' declaration ')'
combinator
         ::= PLUS S*
           | GREATER S*
           | TILDE S*
           | S+
simple_selector_sequence
         ::= ( type_selector | universal ) ( HASH | class | attrib | pseudo | negation | atrule | mediaquery_selector )*
           | ( HASH | class | attrib | pseudo | negation | atrule| mediaquery_selector )+
type_selector
         ::= namespace_prefix? element_name
namespace_prefix
         ::= ( IDENT | '*' )? '|'
element_name
         ::= IDENT
universal
         ::= namespace_prefix? '*'
class    ::= '.' IDENT
printcssrule ::= '@media print' 
mediarule ::= '@media' S conditions ( S? conditions| S?  mediaquery_selector )*
pagerule ::= '@page' S? page_select_group?
arearule::= '@'('bottom-left'|'bottom-center'|'bottom-right'|'top-left'|'top-center'|'top-right'|'left-top'|'left-middle'|'left-bottom'|'right-top'|'right-middle'|'right-bottom'|'bottom-left-corner'|'bottom-right-corner'|'top-left-corner'|'top-right-corner')
atrule  ::= '@' IDENT
conditions ::=  ('and'|'screen'|'or'|'only'|'not'|'amzn-mobi'|'amzn-kf8'|'amzn-mobi7'|',') 
attrib   ::= '[' S* namespace_prefix? IDENT S* ( ( PREFIXMATCH  | SUFFIXMATCH | SUBSTRINGMATCH | '=' | INCLUDES | DASHMATCH ) S* ( IDENT | QUOTED_STRING ) S* )? ']'
pseudo   ::= ':' ':'? ( IDENT | functional_pseudo )
functional_pseudo
         ::= FUNCTION S* expression? ')'
page_select_group ::= page_select ( COMMENT? COMMA S* page_select )*     
page_select ::= (IDENT | IDENT? pagearea+)     
pagearea::= ':'('left'|'right'|'first'|'blank')
property ::= ( QUOTED_STRING | IDENT )
values ::= value (S value | '/' value )* ( S* COMMA S* value (S value | '/' value )* )* important?
value ::= ( URL | ATTR | PLUS | HEX | PERCENTAGE | '-' | DIMENSION | NUMBER  | QUOTED_STRING | IDENT | functional_pseudo) 
expression     ::= ( ( PLUS | PERCENTAGE | URL | ATTR | HEX | '-' | DIMENSION  | NUMBER | QUOTED_STRING | IDENT | COMMA | functional_pseudo) S* )+
negation ::= NOT S* negation_arg S* ')'
negation_arg
         ::= type_selector
           | universal
           | HASH
           | class
           | attrib
           | pseudo
           
COMMENT ::=  '/*' CommentContents '*/' 
URL       ::= 'url(' ( QUOTED_STRING | S* BARE_URL S* ) ')'
ATTR  ::= 'attr('  IDENT  ')'
QUOTED_STRING   ::= ('"' STRING_CONTENT1 '"' | "'" STRING_CONTENT2 "'")
BARE_URL ::= BARE_URL_CHARS

<?TOKENS?>
BARE_URL_CHAR
           ::= #x0021
            | [#x0023-#x0026]
            | [#x002A-#xD7FF]
            | [#xE000-#xFFFD]
            | [#x10000-#x10FFFF]
BARE_URL_CHARS ::= (BARE_URL_CHAR)+
important ::= S* '!' S* 'important'
ident    ::= [-]? nmstart nmchar*
name     ::= nmchar+
nmstart  ::= [_a-zA-Z]
           | nonascii
           | escape
nonascii ::= [^#x0000-#x007F]
unicode  ::= '\' one-to-six-hex-digits ( #x000D #x000A | [ #x000A#x000D#x0009#x000C] )?
escape   ::= unicode
           | '\' [^0-9a-f#x000A#x000D#x000C]
nmchar   ::= [_a-zA-Z0-9#x2D]
           | nonascii
           | escape
num      ::= [0-9]+
           | [0-9]* '.' [0-9]+
nl       ::= #x000A
           | #x000D #x000A
           | #x000D
           | #x000C
STRING_CONTENT1 ::= ( [^#x000A#x000D#x000C\"] | '\' nl | nonascii | escape )*
STRING_CONTENT2 ::= ( [^#x000A#x000D#x000C\'] | '\' nl | nonascii | escape )*
w        ::= [ #x0009#x000D#x000A#x000C]*
N        ::= 'n'
           | '\' zero-to-four-zeroes ( '4e' | '6e' ) ( #x000D #x000A | [ #x0009#x000D#x000A#x000C] )?
           | '\n'
O        ::= 'o'
           | '\' zero-to-four-zeroes ( '4f' | '6f' ) ( #x000D #x000A | [ #x0009#x000D#x000A#x000C] )?
           | '\o'
T        ::= 't'
           | '\' zero-to-four-zeroes ( '54' | '74' ) ( #x000D #x000A | [ #x0009#x000D#x000A#x000C] )?
           | '\t'
S        ::= [ #x0009#x000D#x000A#x000C]+
INCLUDES ::= '~='
DASHMATCH
         ::= '|='
PREFIXMATCH
         ::= '^='
SUFFIXMATCH
         ::= '$='
SUBSTRINGMATCH
         ::= '*='
IDENT    ::= ident
FUNCTION ::= (ident - ('attr'|'url')) '('
NUMBER   ::= '-'?num
HASH     ::= '#' name
HEX      ::= '#' one-to-six-hex-digits
PERCENTAGE ::= '-'? num '%'
PLUS     ::= w '+'
GREATER  ::= w '>'
COMMA    ::= w ','
TILDE    ::= w '~'
NOT      ::= ':' N O T '('
DIMENSION ::= '-'?num S? ( 'ch' | 'cm' | 'em' | 'ex' | 'fr' | 'in' | 'mm' | 'pc' | 'pt' | 'px' | 'Q' | 'rem' | 'vh' | 'vmax' | 'vmin' | 'vw' | 'dpi' )
Char     ::= #x0009
           | #x000A
           | #x000D
           | [#x0020-#xD7FF]
           | [#xE000-#xFFFD]
           | [#x10000-#x10FFFF]
CommentContents
          ::=( ( Char+ - ( Char* ( '/*' | '*/' ) Char* ) ) - ( Char* '/' ) ) &'*'
            | ( Char+ - ( Char* ( '/*' | '*/' ) Char* ) ) &'*/'
one-to-six-hex-digits
         ::= [0-9a-fA-F]
           | [0-9a-fA-F] [0-9a-fA-F]
           | [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F]
           | [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F]
           | [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F]
           | [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F]
zero-to-four-zeroes
         ::=
           | '0'
           | '00'
           | '000'
           | '0000'